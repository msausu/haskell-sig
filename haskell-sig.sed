#!/usr/bin/env -S sed -n -f

/./!                                                                                       {
    d
}
/[[:blank:]]*--[[:blank:]]/                                                                {
    d
}
/{-/                                                                                       {
    s/{-\([[:alnum:][:blank:]_`'"$%&@*+~.:,;/=<>()|#!?^]\|\\\|\[\|\]\)\+-}//g
    s/{-.*$//
    :01 
    /-}/!                                                                                  {
        n
        b 01
    }
    s/^.*-}//
}
/^\([[:alpha:]][[:alnum:]_']*\([[:blank:]]*,[[:blank:]]*[[:alpha:]][[:alnum:]_']*\)*\|([!#$%&*+.:,;/<=>?@\^|-~:]\+)\)[[:blank:]]*\(--[[:blank:]].*\|{-.*\)$/ {
    s/{-\([[:alnum:][:blank:]_`'"$%&@*+~.:,;/=<>()|#!?^]\|\\\|\[\|\]\)\+-}/ /g
    s/{-.*$//
    s/\([[:blank:]]*\)--[[:blank:]].*$/\1/
    h
    :10
    n
    /./!                                                                                {
        b 10
    }
    /^[[:blank:]]\+\({-\([[:alnum:][:blank:]_`'"$%&@*+~.:,;/=<>()|#!?^]\|\\\|\[\|\]\)\+-}\)*\(--[[:blank:]].*\)?$/ {
        b 10
    }
    /[[:blank:]]::[[:blank:]]/!                                                         { 
        d
    }
    H
    x
    s/\n//g
}
/^\([[:alpha:]][[:alnum:]_']*\|([!#$%&@*+.:,;/<=>?@\^|-~:]\+)\)\([[:blank:]]*,[[:blank:]]*[[:alpha:]][[:alnum:]_']*\|([!#$%&@*+.:,;/<=>?@\^|-~:]\+)\)*[[:blank:]]*::\([[:blank:]]\|[[:alnum:]]\|\[\|(\)/ { 
    :20 
    s/{-\([[:alnum:][:blank:]_`'"$%&@*+~.:,;/=<>()|#!?^]\|\\\|\[\|\]\)\+-}//g
    /{-/                                                                                {
        s/{-.*$//
        :30
        /./!                                                                            {
            n
            b 30
        }
        /-}/!                                                                           {
            n
            b 30
        }
        s/^\([[:alnum:][:blank:]_`'"$%@*+~.:,;/=<>()|#!?^]\|\\\|\[\|\]\)*-}//
        b 20
    }
    s/\([[:blank:]]*\)--.*$/\1/
    h
    :40
    n
    /./! {
        b 40
    }
    /^[[:blank:]]/                                                                      { 
        s/{-\([[:alnum:][:blank:]_`'"$%@*+~.:,;/=<>()|#!?^]\|\\\|\[\|\]\)\+-}/ /g
        /{-/                                                                            {
            s/{-.*$//
            :50
            /./!                                                                        {
                n
                b 50
            }
            /-}/!                                                                       {
                n
                b 50
            }
            s/^.*-}//
        }
        s/\([[:blank:]]*\)--.*$/\1/
        H
        b 40
    }
    x
    s/\n//g
    s_\([[:alnum:]]\)::_\1 ::_g
    s_::\([[:alnum:]]\)_:: \1_g
    s/\([^,]\)[[:blank:]]\+,/\1,/g
    s/([[:blank:]]\+/(/g
    s/[[:blank:]]\+)/)/g
    s/\[[[:blank:]]\+/\[/g
    s/[[:blank:]]\+\]/\]/g
    s_,\([^[:blank:]]\)_, \1_g
    s/\([^[:blank:]]\)->/\1 ->/g
    s/->\([^[:blank:]]\)/-> \1/g
    s/( -> )/(->)/g
    s/\([[:alnum:]]\)=>/\1 =>/g
    s/=>\([[:alnum:]]\)/=> \1/g
    s/(, )/(,)/g
    s/^\([[:alpha:]][[:alnum:]_']*\|([!#$%&@*+.:,;/<=>?@\^|-~:]\+)\)[[:blank:]]*::\([[:blank:]]\|[[:alnum:]]\|\[\|(\)/\1 :: \2/
    s/[[:blank:]]\+/ /g
    p
}
